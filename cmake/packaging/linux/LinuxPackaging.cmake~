set(CPACK_GENERATOR "DEB" CACHE STRING "Type of package to build")
# Get distribution id
execute_process(COMMAND lsb_release -irs
COMMAND sed "s/ //"
COMMAND sed "s/Fedora/fc/"
COMMAND tr -d '\n' 
OUTPUT_VARIABLE DISTRIB
OUTPUT_STRIP_TRAILING_WHITESPACE)
execute_process(COMMAND arch OUTPUT_VARIABLE ARCH OUTPUT_STRIP_TRAILING_WHITESPACE)
set(CPACK_PACKAGE_FILE_NAME "${PROJECT_NAME}-${${PROJECT_NAME}_VERSION}-${DISTRIB}-${ARCH}")

set (CPACK_LINUX_PACKAGING_INSTALL_PREFIX /usr/local/medInria CACHE STRING "Prefix where the will package be installed on linux plateforms")  

# Write a postinst, prerm and launcher script for Linux
configure_file(postinst.in ${CMAKE_BINARY_DIR}/linux/postinst)
configure_file(prerm.in ${CMAKE_BINARY_DIR}/linux/prerm)
configure_file(medInria_launcher.sh.in ${CMAKE_BINARY_DIR}/linux/medInria_launcher.sh)

# Install a launcher for medInria with right environment variable
install(PROGRAMS ${CMAKE_BINARY_DIR}/linux/medInria_launcher.sh DESTINATION bin)

set(CPACK_DEBIAN_PACKAGE_CONTROL_EXTRA "${CMAKE_BINARY_DIR}/linux/prerm;${CMAKE_BINARY_DIR}/linux/postinst")
set(CPACK_RPM_POST_INSTALL_SCRIPT_FILE ${CMAKE_BINARY_DIR}/linux/postinst)
set(CPACK_RPM_PRE_UNINSTALL_SCRIPT_FILE ${CMAKE_BINARY_DIR}/linux/prerm)

# Add project to package 
set(CPACK_INSTALL_CMAKE_PROJECTS "${CPACK_INSTALL_CMAKE_PROJECTS};${CMAKE_BINARY_DIR};medInria-superProject;ALL;medInria-superProject") 
foreach(package ${packages}) 
	if(NOT USE_SYSTEM_${package})
		ExternalProject_Get_Property(${package} binary_dir)
		set(CPACK_INSTALL_CMAKE_PROJECTS "${CPACK_INSTALL_CMAKE_PROJECTS};${binary_dir};${package};ALL;${package}")
	endif()
endforeach()
foreach(dir ${PRIVATE_PLUGINS_DIRS})
		set(CPACK_INSTALL_CMAKE_PROJECTS "${CPACK_INSTALL_CMAKE_PROJECTS};${dir};${dir};ALL;${dir}")
endforeach()
